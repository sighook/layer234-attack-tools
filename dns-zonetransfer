#!/usr/bin/perl
# Perform a zone transfer

use strict;
use warnings;
use Net::DNS;

my $hostname = shift or die <<EOF;
Requests a zone transfer (AXFR) from a DNS servers.

Usage: $0 <HOSTNAME>

EOF

my $res  = Net::DNS::Resolver->new;

# Find the nameservers for the domain
my $reply = $res->query($hostname, 'NS');
die 'query failed: ' . $res->errorstring . "\n" unless $reply;

my @nameservers = grep { $_->type eq 'NS' } $reply->answer;
@nameservers    = sort { $a->nsdname cmp $b->nsdname } @nameservers;

my @nsdnames;
push(@nsdnames, $_->nsdname) for @nameservers;

print <<EOF;

Perform a zone transfer for:

\@DOMAIN       $hostname
\@NAMESERVERS  @nsdnames

EOF

for my $nsd (@nsdnames) {
    my $nsdlen  = length $nsd;
    my $hyphlen = (76 / 2) - $nsdlen;

    print   '-' x $hyphlen . "[ $nsd ]" . '-' x $hyphlen . "\n";

    $res->tcp_timeout(20);
    $res->nameservers($nsd);

    my @zone = $res->axfr($hostname);
    warn $res->errorstring . "\n" if $res->errorstring ne 'NOERROR';

    $_->print() for @zone;
    print "\n";
}

# vim:sw=4:ts=4:sts=4:et:cc=80
# End of file
